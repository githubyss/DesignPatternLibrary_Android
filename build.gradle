plugins {
    id 'kotlin-android'
    id 'kotlin-kapt'
}

def isShellApp = project.getName() == rootProject.shellApp
def isLaunchApp = project.getName() == rootProject.launchApp

if (isShellApp) {
    println "isShellApp: ${isShellApp}"
}

if (isLaunchApp) {
    println "isLaunchApp: ${isLaunchApp}"
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}


apply from: "${rootDir}/config/buildConfigs.gradle"
apply from: "${rootDir}/config/optionConfigs.gradle"
apply from: "${rootDir}/config/featureConfigs.gradle"
apply from: "${rootDir}/config/versionConfigs.gradle"
apply from: "${rootDir}/config/signingConfigs.gradle"
apply from: "${rootDir}/config/libConfigs.gradle"

android {
    resourcePrefix 'designpattern_'

    defaultConfig {
        versionCode 1
        versionName "1.0"

        buildConfigField 'String', 'classNameSuffixDatabase', '"SqlServer"'
        buildConfigField 'String', 'classNameSuffixVegetable', '"Transgenic"'
    }

    buildTypes {
        debug {
            if (isLaunchApp) {
                shrinkResources feature.isMinifyEnabled
            }
        }

        release {
            if (isLaunchApp) {
                shrinkResources true
            }
        }
    }

    flavorDimensions 'library'
    productFlavors {
        if (isLaunchApp) {
            app {
                dimension 'library'
                applicationId 'com.githubyss.design_pattern'
                versionCode 1
                versionName "1.0"
                manifestPlaceholders = [APP_NAME: "设计模式库"]
            }
        }
    }

    sourceSets {
        main {
            java {
                srcDirs 'src/main/java'
                srcDirs 'src/main/kotlin'
            }
        }

        if (isLaunchApp) {
            app {
                java {
                    srcDirs 'src/app/java'
                    srcDirs 'src/app/kotlin'
                }
            }
        }

        test {
            java {
                srcDirs 'src/test/java'
                srcDirs 'src/test/kotlin'
            }
        }

        all {
            // set -> println "集合 ${set.name} 的文件是 ${set.java.srcDirs}"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation dependenciesLib.kotlin_reflect
    implementation dependenciesLib.androidx_core_ktx
}